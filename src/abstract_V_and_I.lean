/-
Algebraic geometry M4P33, Jan-Mar 2020, formalised in Lean.

Copyright (c) 2020 Kevin Buzzard
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Alexander Uhlmann, and whoever else in the class
wants to join in.

Thoughts about how much of the ğ• and ğ•€ theory
goes through abstractly.

Note: I think that this file is now pretty much done.
-/

-- This is set theory. But we want to do some type theory as well.
-- Types are a convenient way of gathering mathematical objects
-- into well-defined collections, such as rings.
import data.type.basic

-- We want to prove that the images of ğ• and ğ•€ are in natural
-- bijection with each other so we need the general theory
-- of bijections
import data.equiv.basic

-- the lattice structure on subsets (infinite unions etc)
import data.set.lattice


universes u v -- set theorists can set these both to be 0.
              -- (R : Type 0) means "let R be a set".

-- Let R be a set.
-- For example R could be the ring `k[Xâ‚,â€¦,Xâ‚™]`
variables (R : Type u)
-- When we're talking about ğ• and ğ•€, we will not mention R as part
-- of the notation even though it is playing a role
{R}

-- Let ğ”¸â¿ be another set.
variables (A : Type v)
-- Similarly we will not explicitly mention A most of the time
{A}

-- Let `P` be a way of getting a true/false statement from a pair of
-- elements f âˆˆ R and x âˆˆ ğ”¸â¿. For example P(f,x) can be
-- the statement that `f(x) = 0`. You can think of `P` as being a subset
-- of R Ã— ğ”¸â¿ if you like.
variable (P : R â†’ A â†’ Prop)
include P

-- Let ğ•, a function from subsets of R to subsets of
-- ğ”¸â¿, and ğ•€, a function from subsets of ğ”¸â¿
-- to subsets of R, be defined in the usual way.
-- One can think of ğ•(S) as being the largest U such that S Ã— U âŠ† P,
-- and ğ•€(U) as being the largest S such that S Ã— U âŠ† P.

-- The main theorem we will prove today is

-- *Theorem* For all S âŠ† R, ğ•(ğ•€(ğ•(S)))=ğ•(S),
-- assuming some irrelevant extra hypotheses, such as the assumption
-- that k is algebraically closed, or S is an ideal.

def ğ•_ (S : set R) : set A :=
{x : A | âˆ€ f âˆˆ S, P f x}
notation `ğ•`:max := ğ•_ (by exact P)

-- Type of ğ•_ is Î  {R : Type*} {A : Type*}, (R â†’ A â†’ Prop) â†’ set R â†’ set A
-- i.e. (R â†’ A â†’ Prop) â†’ (R â†’ Prop) â†’ (A â†’ Prop)

def ğ•€_ (X : set A) : set R :=
{f : R | âˆ€ x âˆˆ X, P f x}
notation `ğ•€`:max := ğ•€_ (by exact P)

-- restate definitions
lemma mem_ğ•_iff (S : set R) (x : A) : x âˆˆ ğ• S â†” âˆ€ f âˆˆ S, P f x := iff.rfl
lemma mem_ğ•€_iff (V : set A) (f : R) : f âˆˆ ğ•€ V â†” âˆ€ x âˆˆ V, P f x := iff.rfl

-- we're going to be doing set theory
open set

/-- ğ•(âˆ…) is everything. -/
lemma ğ•_empty : ğ• âˆ… = univ :=
begin
  -- It suffices to show that every x of type A is in ğ• âˆ…
  rw eq_univ_iff_forall,
  -- so let x be in A
  intro x,
  -- we need to show that every f in the empty set satisfies (f,x) âˆˆ P
  rw mem_ğ•_iff,
  -- so say f is in the empty set
  intros f hf,
  -- but there are no elements in the empty set so we must be done
  cases hf,
end

/-- ğ•€(âˆ…) is everything -/
lemma ğ•€_empty : ğ•€ âˆ… = univ :=
-- computer science proof
eq_univ_iff_forall.2 $ Î» x f, by rintro âŸ¨âŸ©

-- ğ• is inclusion-reversing
lemma ğ•_antimono (S T : set R) (h : S âŠ† T) : ğ• T âŠ† ğ• S :=
begin
  -- say x âˆˆ ğ•(T)
  intros x hx,
  -- and s in S,
  intros s hs,
  -- We want to show P(s,x) is true, or (s,x) âˆˆ P or s(x) = 0 or however
  -- you think about it.
  -- Because x âˆˆ ğ•(T), we know P(t,x) is true for all t âˆˆ T,
  -- so it suffices to prove s âˆˆ T
  apply hx,
  -- But S âŠ† T
  apply h,
  -- and s âˆˆ S so we're done
  exact hs
end

-- Here is how a computer scientist would write this proof:
lemma ğ•_antimono' (S T : set R) (h : S âŠ† T) : ğ• T âŠ† ğ• S :=
Î» x hx s hs, hx _ (h hs)

-- The advantage of writing it this way is that it also proves the converse!
lemma ğ•€_antimono (U V : set A) (h : U âŠ† V) : ğ•€ V âŠ† ğ•€ U :=
Î» x hx s hs, hx _ (h hs)

-- Exercise: prove ğ•€_antimono the way a mathematician would, using only
-- intros, apply and exact. Need help? Try the natural number game.

-- Thanks to Alexander Uhlmann for spotting definition 3.1 here:
-- https://ncatlab.org/nlab/show/Galois+connection#GaloisTheory
lemma is_galois_connection {Y : set A} {J : set R} :
Y âŠ† ğ• J â†” ğ•€ Y âŠ‡ J :=
begin
  split,
  { intros hY f hf,
    rw mem_ğ•€_iff,
    intros x hx,
    exact hY hx f hf,
  },
  { intros hJ x hx f hf,
    exact hJ hf x hx,
  }
end

lemma ğ•_union (S T : set R) : ğ• (S âˆª T) = ğ• S âˆ© ğ• T :=
begin
  -- we prove both inclusions
  apply set.subset.antisymm,
  { -- say x âˆˆ ğ•(S âˆª T)
    intros x hx,
    -- we need to prove x âˆˆ ğ• S and x âˆˆ ğ• T
    split,
    -- both of these follow easily from ğ•_antimono
    -- exact ğ•_antimono _ _ _ _ hx, -- TODO(kmb)
        -- why is the wrong underscore marked in red??
      exact ğ•_antimono _ _ _ (subset_union_left _ _) hx,
      exact ğ•_antimono _ _ _ (subset_union_right _ _) hx,
  },
  { -- say x âˆˆ ğ•(S) âˆ© ğ•(T)
    rintros x âŸ¨hxS, hxTâŸ©,
    -- we need to prove that for all f âˆˆ S âˆª T, f(x) = 0
    intros f hf,
    -- well f is either in S or T (or both)
    cases hf,
    { -- and if f âˆˆ S then we're done because x âˆˆ ğ•(S)
      exact hxS _ hf
    },
    { -- whereas if f âˆˆ T then we're done because x âˆˆ ğ•(T)
      exact hxT _ hf
    }
  }
end 

-- We prove this one in a slightly different way.
lemma ğ•€_union (W X : set A) : ğ•€ (W âˆª X) = ğ•€ W âˆ© ğ•€ X :=
begin
  -- By extensionality, two sets are equal iff they have the same elements
  ext x,
  -- To be in the intersection of two sets just means being in both of them
  show _ â†” x âˆˆ ğ•€ W âˆ§ x âˆˆ ğ•€ X,
  -- By the definition of ğ•€,...
  rw [mem_ğ•€_iff, mem_ğ•€_iff, mem_ğ•€_iff],
  --- we have to prove that 
  -- W âˆª X âŠ† {f : f(x) = 0} iff W âŠ† {f : f(x) = 0} and X âŠ† {f : f(x) = 0}
  show W âˆª X âŠ† _ â†” W âŠ† _ âˆ§ X âŠ† _, -- the underscore just means "guess the set"
  -- We prove the iff by proving both directions separately
  split,
  { -- Here we prove W âˆª X âŠ† Z â†’ W âŠ† Z âˆ§ X âŠ† Z
    intro hWX,
    split,
      refine set.subset.trans _ hWX, apply subset_union_left,
    refine set.subset.trans _ hWX, apply subset_union_right,
  },
  { -- and here we prove W âŠ† Z âˆ§ X âŠ† Z â†’ W âˆª X âŠ† Z
    rintros âŸ¨hW, hXâŸ©, apply union_subset; assumption
  }
end

lemma ğ•_Union (Î¹ : Type*) (S : Î¹ â†’ set R) : ğ• (â‹ƒ i, S i) = â‹‚ i, ğ• (S i) :=
begin
  -- two sets are equal iff they have the same elements
  ext x,
  -- To be in the intersection of a bunch of sets just means
  -- being in all of them
  rw mem_Inter,
  -- By the definition of ğ•,
  rw [mem_ğ•_iff],
  show (âˆ€ (f : R), (f âˆˆ â‹ƒ (i : Î¹), S i) â†’ P f x) â†” âˆ€ (i : Î¹), âˆ€ f âˆˆ S i, P f x,
  -- Now prove both inclusions separately
  split,
    intros,
    apply a,
    rw mem_Union,
    use i,
    assumption,
  intros h f hf,
  rw mem_Union at hf,
  cases hf with i hi,
  apply h i,
  assumption,
end

-- an AI can find a proof of this too:
lemma ğ•€_Union (Î¹ : Type*) (J : Î¹ â†’ set A) : ğ•€ (â‹ƒ i, J i) = â‹‚ i, ğ•€ (J i) :=
begin 
  ext,
  simp [ğ•€_],
  tauto,
end


lemma ğ•ğ•€_mono (U V : set A) (h : U âŠ† V) : ğ• (ğ•€ U) âŠ† ğ• (ğ•€ V) :=
begin
  -- ğ• is anti-monotonic
  apply ğ•_antimono,
  -- and ğ•€ is too
  apply ğ•€_antimono,
  -- so we just have to prove U âŠ† V, which is an assumption
  exact h
end

-- computer science proof of the other direction
lemma ğ•€ğ•_mono (S T : set R) (h : S âŠ† T) : ğ•€ (ğ• S) âŠ† ğ•€ (ğ• T) :=
ğ•€_antimono P _ _ (ğ•_antimono P _ _ h)

-- During the lecture today (17/01/20), it was pointed out that ğ•(S) was the
-- largest U such that S Ã— U was a subset of P, and ğ•€(U) was the largest S
-- such that S Ã— U was a subset of P. This geometric way of thinking
-- about things makes the next lemma trivial. Can you understand the Lean proof?

/-- U âŠ† ğ•(ğ•€(U)) -/
lemma sub_ğ•ğ•€ (U : set A) : U âŠ† ğ• (ğ•€ U) :=
begin
  intros x hx,
  rw mem_ğ•_iff,
  intros f hf,
  rw mem_ğ•€_iff at hf,
  apply hf,
  exact hx,
end

-- Because the proofs of sub_ğ•ğ•€ and sub_ğ•€ğ• are basically
-- the same, it might come as no surprise to see that you
-- can prove one of them using the other one! The trick is
-- to make sure you allow quantification over all R and A
-- so you can switch them around.

/-- S âŠ† ğ•€(ğ•(S)) -/
lemma sub_ğ•€ğ• (S : set R) : S âŠ† ğ•€ (ğ• S) :=
sub_ğ•ğ•€ _ _

-- the big theorem
lemma ğ•ğ•€ğ•_eq_ğ• (S : set R) : ğ• (ğ•€ (ğ• S)) = ğ• S :=
begin
  apply set.subset.antisymm,
  { apply ğ•_antimono,
    apply sub_ğ•€ğ•
  },
  { apply sub_ğ•ğ•€,
  }
end

-- the same theorem again (permute R and A)
lemma ğ•€ğ•ğ•€_eq_ğ•€ (V : set A) : (ğ•€ (ğ• (ğ•€ V))) = ğ•€ V :=
ğ•ğ•€ğ•_eq_ğ• _ V -- same proof but with a different P

open set

-- this final proof is written in a very computer-science way
/-- The images of ğ• and of ğ•€ are naturally in bijection -/
lemma not_the_nullstellensatz : {V // âˆƒ J, ğ• J = V} â‰ƒ {I // âˆƒ V, ğ•€ V = I} :=
{ to_fun := Î» V, âŸ¨ğ•€ (V.1), V, rflâŸ©,
  inv_fun := Î» I, âŸ¨ğ• I.1, I, rflâŸ©,
  left_inv := begin
    rintro âŸ¨V, J, hJâŸ©,
    rw subtype.ext,
    change ğ• (ğ•€ V) = V,
    rw â†hJ,
    refine ğ•ğ•€ğ•_eq_ğ• _ _,
  end,
  right_inv := begin
    rintro âŸ¨J, V, hVâŸ©,
    rw subtype.ext,
    change ğ•€ (ğ• J) = J,
    rw â†hV,
    refine ğ•€ğ•ğ•€_eq_ğ•€ _ _,
  end
}

-- The Nullstellensatz says that the image of ğ•€ is precisely the
-- radical ideals. One inclusion is clear (which?)