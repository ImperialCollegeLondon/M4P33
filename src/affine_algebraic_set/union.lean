/-
Algebraic geometry lecture 1:

The union of two algebraic sets is an algebraic set.

Kevin Buzzard
-/

import affine_algebraic_set.basic -- the basic theory of affine algebraic sets.

/-!
# The union of two affine algebraic sets is affine.

Let k be a field and let n be a natural number. We prove the following
theorem in this file:

Theorem. If V and W are two affine algebraic subsets of kâ¿
then their union V âˆª W is also an affine algebraic subset of kâ¿.

Lean version:

`def union (V W : affine_algebraic_set k n) : affine_algebraic_set k n`

Maths proof: if V is cut out by the set S âŠ† k[X_1,X_2,â€¦,X_n]
and W is cut out by T, then we claim V âˆª W is cut out by the set ST := { s*t | s âˆˆ S and t âˆˆ T}.
To prove that the set cut out by ST equals V âˆª W, we prove both inclusions separately.

One inclusion is very easy. If x âˆˆ V âˆª W then either every element of S vanishes at x or every
element of T vanishes on x, and either way every element of ST vanishes at x.

Conversely, if x vanishes at every element of ST, then we want to prove that x âˆˆ V âˆª W. If x is
in V then we're done. If not, then this means that there exists some s âˆˆ S with s(x) â‰  0. 
Then for every t âˆˆ T, we have s(x)t(x)=st(x)=0, and hence t(x)=0, which implies that x is in W.

## Implementation notes

I defined an affine algebraic set to be the zeros of an arbitrary
set of functions, as opposed to just a finite set. We will see later
on that these definitions are equivalent.

If V is an affine algebraic set, then V is a pair. The first
element of the pair is a subset V.carrier âŠ† kâ¿, but we will call this set V as well (there is
a coercion from affine algebraic sets to subsets of kâ¿).
The second element of the pair is the proof that there exists a subset S of k[X_1,X_2,...,X_n]
such that V is cut out by S, by which I mean that V is the set of x âˆˆ k^n which vanish
at each element of S.

## References

Martin Orr's lecture notes at https://homepages.warwick.ac.uk/staff/Martin.Orr/2017-8/alg-geom/

## Tags

algebraic geometry, algebraic variety
-/

-- end of docstring; code starts here. 

-- We're proving theorems about affine algebraic sets so the names of the theorems
-- should start with "affine_algebraic_set".
namespace affine_algebraic_set

-- let k be a field
variables {k : Type*} [discrete_field k]

-- and let Ïƒ be any set e.g. {1,2,3,...,n}
variable {Ïƒ : Type*}

-- We're working with multivariable polynomials, so let's get access to their notation
open mv_polynomial

-- Now here's a basic fact about affine algebraic sets.

/-- The union of two algebraic subsets of kâ¿ is an algebraic subset-/
def union (V W : affine_algebraic_set k Ïƒ) : affine_algebraic_set k Ïƒ :=
{ carrier := V âˆª W, -- the underlying set is the union of the two sets defining V and W
  is_algebraic' :=
  -- We now need to prove that the union of V and W is cut out by some set of polynomials.
  begin
    -- Now here's the bad news. 

    -- Lean notation for kâ¿ is `Ïƒ â†’ k`.
    -- Lean notation for k[Xâ‚, Xâ‚‚, ..., Xâ‚™] is `mv_polynomial Ïƒ k`.
    -- Lean notation for the subsets of X is `set X`

    -- Let's state what we're trying to prove, using Lean's notation.
    show 
    âˆƒ (U : set (mv_polynomial Ïƒ k)),
      -- such that
      (V : set _) âˆª W = ğ• U,
    -- say S is the set that defines V
    cases V.is_algebraic with S hS,
    -- and T is the set that defines W
    -- (slightly fancier way)
    rcases W.is_algebraic with âŸ¨T, hTâŸ©,
    -- Now reduce to an unwieldy precise statement about zeros of polynomials
    rw [hS, hT],
    -- Our goal is now to come up with a set U such that
    -- the zeros of U are exactly the union of the zeros of S and of T.
    -- Here's how to do it.
    use {u | âˆƒ (s âˆˆ S) (t âˆˆ T), u = s * t},
    -- The goal in maths is now
    -- â‹‚ (s âˆˆ S) zeros(s) âˆª â‹‚ (t âˆˆ T) zeros(t) = â‹‚ (u âˆˆ ST) zeros(u).
    -- Lean manages to make quite a mess of its display of this. 
    
    -- To prove that the affine algebraic set cut out by this collection of polynomials
    -- is precisely the set V âˆª W, we check both inclusions.
    apply set.subset.antisymm,
    { -- Here's the easier of the two inclusions.
      -- say x âˆˆ V âˆª W,
      intros x hx, rw mem_ğ•_iff,
      -- it's either in V or W.
      cases hx with hxV hxW,
      { -- Say x âˆˆ V
        -- We know that x vanishes at every element of S.
        rw mem_ğ•_iff at hxV,
        -- We want to prove x vanishes at every polynomial of the form s * t
        -- with s âˆˆ S and t âˆˆ T.
        -- so let's take an element u of the form s * t
        rintro u,
        -- Let's now notice that the goal has got completely out of hand, and
        -- simplify it back to âˆ€ s âˆˆ S and âˆ€ t âˆˆ T, (s * t)(x) = 0.
        suffices : âˆ€ s âˆˆ S, âˆ€ t âˆˆ T, u = s * t â†’ u.eval x = 0,
        {rintros âŸ¨s, hsS, t, htT, rflâŸ©, exact this s hsS t htT rfl},
        rintro s hs t ht rfl,
        -- we need to show st(x)=0.
        -- Because x âˆˆ V, we have s(x)=0. 
        have hx := (hxV s) hs,
        change s.eval x = 0 at hx,
        -- It suffices to show s(x)*t(x)=0
        rw eval_mul,
        -- but s(x) = 0,
        rw hx,
        -- and now it's obvious
        apply zero_mul,
      },
      { -- This is the case x âˆˆ W and it's essentially completely the same as the x âˆˆ V argument so I won't
        -- comment it. Some sort of argument with the `wlog` tactic might be able to do this.
        rw mem_ğ•_iff at hxW,
        rintro u,
          suffices : âˆ€ s âˆˆ S, âˆ€ t âˆˆ T, u = s * t â†’ u.eval x = 0,
          { rintros âŸ¨s, hsS, t, htT, rflâŸ©, exact this s hsS t htT rfl},
        rintro s hs t ht rfl,
        have hx := hxW t,
        replace hx : eval x t = 0 := hx ht,
        rw eval_mul,
        rw hx, simp,
      }
    },
    { -- This is the harder way; we need to check that if x vanishes on every element of S*T, 
      -- then x âˆˆ V or x âˆˆ W.
      intros x hx, rw mem_ğ•_iff at hx,
      have hx' : âˆ€ u s : mv_polynomial Ïƒ k, s âˆˆ S â†’ âˆ€ t âˆˆ T, u = s * t â†’ u.eval x = 0,
        simpa using hx,
      classical, -- We now proudly assume the law of the excluded middle.
      -- If x âˆˆ V then the result is easy...
      by_cases hx2 : x âˆˆ (V : set _),
        left, rwa â†hS,
      -- ...so we can assume assume x âˆ‰ V,
      -- and hence that there's s âˆˆ S such that s(x) â‰  0
      rw [hS, mem_ğ•_iff, not_forall] at hx2,
      cases hx2 with s hs,
      have hs2 : s âˆˆ S âˆ§ Â¬eval x s = 0,
        simpa using hs,
      cases hs2 with hsS hns,
      -- we now show x âˆˆ W
      right,
      rw mem_ğ•_iff,
      -- Say t âˆˆ T
      intro t,
      -- We want to prove that t(x) = 0.
      suffices : t âˆˆ T â†’ eval x t = 0,
        simpa,
      intro ht,
      -- Now by assumption, x vanishes on s * t. 
      replace hx' := hx' (s * t) s hsS t ht rfl,
      -- so s(x) * t(x) = 0
      rw eval_mul at hx',
      -- so either s(x) or t(x) = 0, but we chose s such that s(x) â‰  0.
      cases mul_eq_zero.1 hx' with hxs hxt,
        -- So the case s(x) = 0 is a contradiction
        contradiction,
      -- and t(x) = 0 is what we wanted to prove
      assumption
    }
  end
}
end affine_algebraic_set
