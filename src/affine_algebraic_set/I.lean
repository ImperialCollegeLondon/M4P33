/-
Copyright (c) 2020 Kevin Buzzard and 
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, and whoever else wants to join in.
-/

-- imports the theory of multivariable polynomials over rings
import data.mv_polynomial
import for_mathlib.mv_polynomial

/-!
# The ğ•€ construction

Let k be a commutative ring and let n be a natural number.

This file defines the map ğ• from subsets of k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™]
to affine algebraic sets in kâ¿, and the map ğ•€ from affine algebraic
sets in kâ¿ to ideals of k[Xâ‚,Xâ‚‚,â€¦, Xâ‚™]. It proves basic properties
about them which can be shown without the Nullstellensatz.

To get ğ• in VS Code, type `\bbV`.

Note: we never assume that the number of variables is finite,
so actually instead of using a natural number n, we use an
arbitrary set N for our variables.

All the definitions work for k a commutative ring, but not all
of the the theorems do. But computer scientists want us to set
up the theory for commutative rings and mathematicians should
learn to think more like computer scientists. 
So k starts off being a commutative ring.

## Important definitions

* `ğ• : set (mv_polynomial n k) â†’ set (n â†’ k)` 
  sending a subset S of k[Xâ‚,Xâ‚‚,â€¦Xâ‚™] to the subset of kâ¿ cut out by the zeros of all
  the elements of S.

## 

## References

Martin Orr's lecture notes at
https://homepages.warwick.ac.uk/staff/Martin.Orr/2017-8/alg-geom/

## Tags

algebraic geometry, algebraic variety, ğ•
-/

-- start of file 
-- We're dealing with multivariable polynomials so let's open
-- the namespace to get easy access to all the functions
open mv_polynomial

-- let k be a commutative ring
variables {k : Type*} [comm_ring k]

-- and let n be any set, but pretend it's {1,2,...,n}.
-- We'll work with polynomials in variables X_i for i âˆˆ n.
variable {n : Type*}

/- Interlude: Lean 3 notation hell.

* the multivariable polynomial ring k[Xâ‚, Xâ‚‚, ..., Xâ‚™] is denoted
  `mv_polynomial n k`.

* The set kâ¿ is denoted
  `n â†’ k`
  (which means maps from n to k, and if you're thinking
   about n as {1,2,3,...,n} then you can see that this makes sense).

* subsets of a set X are denoted
  `set X`

* To evaluate a polynomial f on a vector x, we seem to have to write
  `eval x f`
  Note the order! "Maps on the right".

-/

namespace affine_algebraic_set

/-- ğ•€ : the function sending a subset of kâ¿ to
  a subset of k[Xâ‚,Xâ‚‚,â€¦Xâ‚™], defined in Martin Orr's notes. -/
def ğ•€ (X : set (n â†’ k)) : set (mv_polynomial n k) :=
{f | âˆ€ x âˆˆ X, eval x f = 0 }

open set

/-- f âˆˆ ğ•€ X â†” for all x âˆˆ X, f(x) = 0. This is true by definition. -/
lemma mem_ğ•€_iff {X : set (n â†’ k)} {f : mv_polynomial n k} :
  f âˆˆ ğ•€ X â†” âˆ€ x âˆˆ X, eval x f = 0 := iff.rfl

-- proofs which a mathematician might get something from start here

/-- ğ•€ âˆ… = all of k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™] -/
lemma empty : ğ•€ (âˆ… : set (n â†’ k)) = univ :=
begin
  -- we have to prove that every f âˆˆ k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™] is in ğ•€ âˆ… 
  rw eq_univ_iff_forall,
  -- so say f âˆˆ k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™] is arbitrary
  intro f,
  -- and we need to prove f âˆˆ ğ•€ âˆ…. 
  -- Equivalently, we need to prove that for all x âˆˆ âˆ…, f(x) = 0
  rw mem_ğ•€_iff,
  -- so say x is in the empty set
  intros x hx,
  -- and now we have a contradiction, so we can prove anything
  cases hx
end

lemma univ {k : Type*} [integral_domain k] [infinite k] {n : Type*} :
  ğ•€ (univ : set (n â†’ k)) = {0} :=
begin
  -- we prove inclusions in both directions
  apply set.subset.antisymm,
  { -- this way is the slightly tricky way;
    -- we need to prove that ğ•€ (kâ¿) âŠ† 0
    -- so say f âˆˆ ğ•€ (kâ¿) is arbitrary
    intros f hf,
    -- and we need to prove it's zero
    rw mem_singleton_iff,
    -- -> only poly that's zero everywhere is zero poly
    rw mem_ğ•€_iff at hf,
    rw â†mv_polynomial.eval_eq_zero,
    intro x, apply hf x, apply mem_univ,
  },
  { 
    -- unpack set
     rw singleton_subset_iff,
     -- apply definition of ğ•€ 
     rw mem_ğ•€_iff,
     simp,
  },
end

lemma ğ•€_antimono (V W : set (n â†’ k)) :
  V âŠ† W â†’ ğ•€ W âŠ† ğ•€ V :=
begin
  -- Assume V âŠ† W and f a polynomial
  intros H f,
  -- Apply Definition of ğ•€ twice
  rw [mem_ğ•€_iff,mem_ğ•€_iff],
  -- More Assumptions
  intro P,
  intros x HX,
  -- Use that f(x)=0 âˆ€ xâˆˆW
  apply P,
  -- Use V âŠ† W
  from H HX,
end

-- The image of ğ•€ is an ideal

-- We do this by defining a function ğ•€' from subsets of kâ¿ to ideals of R,
-- and showing that ğ•€' agrees with ğ•€ when you take the ideal and consider
-- it as a set 
noncomputable def ğ•€' (X : set (n â†’ k)) : ideal (mv_polynomial n k) :=
{ carrier := ğ•€ X, -- underlying set is just ğ•€(X)
  zero := by simp [ğ•€], -- zero is obviously in.
  add := begin
    -- Goal: if f and g are in ğ•€(X), then so is f + g.
    -- say f and g are elements of ğ•€(X).
    rintros f g hf hg,
    -- We know f(x) = 0 for all x âˆˆ X and g(x) = 0 for all x âˆˆ X
    change âˆ€ (x : n â†’ k), x âˆˆ X â†’ eval x f = 0 at hf,
    change âˆ€ (x : n â†’ k), x âˆˆ X â†’ eval x g = 0 at hg,
    -- so now say x âˆˆ X.
    intros x hx,
    -- We want to prove (f + g)(x)=0. But (f + g)(x)=f(x) + g(x)
    rw eval_add,
    -- so (f + g)(x) = 0 + 0
    rw [hf _ hx, hg _ hx],
    -- which is 0
    apply zero_add
  end,
  smul := begin
    -- goal: if f âˆˆ ğ•€(X) and c âˆˆ k[Xâ‚,Xâ‚‚,â€¦,X_â‚™], then cf âˆˆ ğ•€(X).
    rintros c f hf,
    -- We know f(x) = 0 for all x âˆˆ X.
    change âˆ€ (x : n â†’ k), x âˆˆ X â†’ eval x f = 0 at hf,
    -- Let's choose x âˆˆ X
    intros x hx,
    -- We need to prove cf(x)=0. 
    -- But cf(x)=c(x)f(x)
    rw [smul_eq_mul, eval_mul],
    -- and f(x)=0
    rw hf _ hx,
    -- so we're done
    rw mul_zero
    -- refl omitted because Lean rw is clever
  end }

  lemma ğ•€_eq_ğ•€' (X : set (n â†’ k)) : (ğ•€' X : set (mv_polynomial n k)) = ğ•€ X := rfl

end affine_algebraic_set