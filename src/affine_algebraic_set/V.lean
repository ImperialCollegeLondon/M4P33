/-
Algebraic geometry M4P33, Jan-Mar 2020, formalised in Lean.

Copyright (c) 2020 Kevin Buzzard
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, and whoever else in the class wants to join in.

Note: if you are viewing this file in a browser via the following
link: 

https://leanprover-community.github.io/lean-web-editor/#url=https%3A%2F%2Fraw.githubusercontent.com%2FImperialCollegeLondon%2FM4P33%2Fmaster%2Fsrc%2Faffine_algebraic_set%2FV.lean

then you can click around on the code and see the state of Lean's "brain"
at any point within any begin/end proof block.
-/

-- imports the theory of multivariable polynomials over rings
import data.mv_polynomial

/-!
# Lecture 2 : The ð• construction

Let k be a commutative ring and let n be a natural number.

This file defines the map ð• from subsets of k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™]
to subsets of kâ¿, and proves basic properties about this map.

To get ð• in VS Code, type `\bbV`.

Note: we never assume that the number of variables is finite,
so actually instead of using a natural number n, we use an
arbitrary set n for our variables.

All the definitions work for k a commutative ring, but not all
of the the theorems do. However, computer scientists want us to set
up the theory in as much generality as possible, and I believe that
mathematicians should learn to think more like computer scientists. 
So k starts off being a commutative ring, and occasionally changes later.

## Lean 3 notation: important comments.

Because we're not using Lean 4, we will have to deal with some
awkward notational issues.

* the multivariable polynomial ring k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™] is denoted
  `mv_polynomial n k`.

* The set kâ¿ is denoted
  `n â†’ k`.

  (note: this means maps from n to k, and if you're thinking
   about n as {1,2,3,...,n} then you can see that this makes sense).

* subsets of a set X are denoted
  `set X`

* The subset of X which is all of X is not called X :-) It's called
  `univ`

* To evaluate a polynomial f on a vector x, we write
  `eval x f`

  Note the order! "Maps on the right".

## Important definitions

* `ð• : set (mv_polynomial n k) â†’ set (n â†’ k)` 
  sending a subset S of k[Xâ‚,Xâ‚‚,â€¦Xâ‚™] to the subset of kâ¿ cut out
  by the zeros of all the elements of S.

## References

Martin Orr's lecture notes at
  https://homepages.warwick.ac.uk/staff/Martin.Orr/2017-8/alg-geom/

## Tags

algebraic geometry, algebraic variety, ð•
-/

-- code starts here

-- We're dealing with multivariable polynomials so let's open the
-- namespace to get easy access to all the functions
open mv_polynomial

-- let k be a commutative ring
variables {k : Type*} [comm_ring k]

-- and let n be any set, but pretend it's {1,2,...,n} with n a natural number.
-- We'll work with polynomials in variables X_i for i âˆˆ n.
variable {n : Type*}

/- recall:

     Maths                 Lean 3
     ---------------------------------------
     k[Xâ‚, Xâ‚‚, ..., Xâ‚™]    mv_polynomial n k
     kâ¿                    n â†’ k
     subsets of X          set X
     the subset X of X     univ
     f(x)                  eval x f
-/

/-- ð• : the function sending a subset S of k[Xâ‚,Xâ‚‚,â€¦Xâ‚™] to
  the subset of kâ¿ defined as the intersection of the zeros of all
  the elements of S. For more details, see Martin Orr's notes -/
def ð• (S : set (mv_polynomial n k)) : set (n â†’ k) :=
{x : n â†’ k | âˆ€ f âˆˆ S, eval x f = 0}

-- Now let's prove a bunch of theorems about ð•, in a namespace

namespace affine_algebraic_set

-- the theorems will be about sets, so let's open the set namespace
-- giving us easier access to theorems about sets 

open set

-- The following lemma has a trivial proof so don't worry about it.
/-- x âˆˆ ð• S â†” for all f âˆˆ S, f(x) = 0. This is true by definition. -/
lemma mem_ð•_iff {S : set (mv_polynomial n k)} {x : n â†’ k} :
  x âˆˆ ð• S â†” âˆ€ f âˆˆ S, eval x f = 0 := iff.rfl

-- The rest of the proofs in this file are supposed to be comprehensible
-- to mathematicians 

/-- ð•(âˆ…) = kâ¿ -/
lemma ð•_empty : ð• (âˆ… : set (mv_polynomial n k)) = univ :=
begin
  -- We need to show that for all x in kâ¿, x âˆˆ ð• âˆ…
  rw eq_univ_iff_forall,
  -- so say x âˆˆ kâ¿.
  intro x,
  -- By definition of ð•, we need to check that f(x) = 0 for all f in âˆ…
  rw mem_ð•_iff,
  -- so say f is a polynomial
  intro f,
  -- and f is in the empty set
  intro hf,
  -- well, our assumptions give a contradiction,
  -- and we can deduce anything from a contradiction
  cases hf,
end

/-- Over a non-zero commutative ring, ð• (k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™]) = âˆ… -/
lemma ð•_univ {k : Type*} [nonzero_comm_ring k] {n : Type*} :
  ð• (univ : set (mv_polynomial n k)) = âˆ… :=
begin
  -- It suffices to show that for all x âˆˆ kâ¿, x isn't in ð• (all polynomials)
  rw eq_empty_iff_forall_not_mem,
  -- so say x âˆˆ kâ¿
  intro x,
  -- we need to check that it's not true that for every polynomial f, f(x) = 0
  rw mem_ð•_iff,
  -- so let's assume that f(x) = 0 for every polynomial f,
  intro h,
  -- and get a contradiction (note that the goal is now `false`).
  -- Let's consider the constant polynomial 1; we deduce 1(x) = 0.
  replace h := h (C 1) (mem_univ _),
  -- evaluating 1 at x gives the value 1
  rw eval_C at h,
  -- so 1 = 0 in k, which contradicts k being non-zero
  exact zero_ne_one h.symm 
end

/-- ð•({0}) = kâ¿ -/
lemma ð•_zero : ð• ({0} : set (mv_polynomial n k)) = univ :=
begin
  -- It suffices to prove every element of kâ¿ is in ð•(0)
  rw eq_univ_iff_forall,
  -- so say x âˆˆ kâ¿
  intro x,
  -- To prove it's in V(0), we need to show f(x)=0 for all f in {0} 
  rw mem_ð•_iff,
  -- so take f in {0}
  intros f hf,
  -- Then it's zero!
  rw mem_singleton_iff at hf, 
  -- so we have to prove 0(x) = 0
  rw hf,
  -- which is obvious
  refl,
end

/-- If k â‰  0 then ð•({1}) = âˆ… -/
lemma ð•_one {k : Type*} [nonzero_comm_ring k] {n : Type*} :
  ð• ({1} : set (mv_polynomial n k)) = âˆ… :=
begin
  -- this is basically the same proof as ð•_univ
  -- It suffices to show that for all x âˆˆ kâ¿, x isn't in ð• ({1})
  rw eq_empty_iff_forall_not_mem,
  -- so say x âˆˆ kâ¿
  intro x,
  -- we need to check that it's not true that for all f âˆˆ {1}, f(x) = 0
  rw mem_ð•_iff,
  -- so let's assume that f(x) = 0 for every polynomial f in {1},
  intro h,
  -- and get a contradiction (note that the goal is now `false`).
  -- Setting f = 1, we deduce 1(x) = 0.
  replace h := h (C 1) (mem_singleton _),
  -- evaluating the polynomial 1 at x gives the value 1
  rw eval_C at h,
  -- so 1 = 0 in k, which contradicts k being non-zero
  exact zero_ne_one h.symm 
end

/-- If S âŠ† T then ð•(T) âŠ† ð•(S) -/
theorem ð•_antimono (S T : set (mv_polynomial n k)) :
  S âŠ† T â†’ ð• T âŠ† ð• S :=
begin
  -- We are assuming S âŠ† T
  intro hST,
  -- Let x âˆˆ ð• T be arbitrary 
  intros x hx,
  -- We want to prove x âˆˆ ð• S.
  -- We know that âˆ€ t âˆˆ T, t(x) = 0, and we want to
  -- prove that âˆ€ s âˆˆ S, s(x) = 0. 
  rw mem_ð•_iff at hx âŠ¢,
  -- So say s âˆˆ S.
  intros s hs,
  -- we want to prove s(x) = 0.
  -- But t(x) = 0 for all t in T, so it suffices to prove s âˆˆ T
  apply hx,
  -- and this is clear because S âŠ† T
  exact hST hs
end

theorem ð•_union (S T : set (mv_polynomial n k)) :
ð• (S âˆª T) = ð• S âˆ© ð• T :=
begin
  -- let's prove this equality of sets by proving âŠ† and âŠ‡
  apply set.subset.antisymm,
  { -- Step 1: we prove the inclusion ð• (S âˆª T) âŠ† ð• S âˆ© ð• T.
    -- So let x be an element of the LHS
    intros x hx,
    -- then x âˆˆ ð• (S âˆª T) so âˆ€ f âˆˆ S âˆª T, f(x) = 0. Call this hypothesis `hx`.
    rw mem_ð•_iff at hx,
    -- To prove x âˆˆ ð• S âˆ© ð• T, it suffices to prove x âˆˆ ð• S and x âˆˆ ð• T
    split,
    { -- We deal with the two cases separately.
      -- To prove x âˆˆ ð• S, we need to show that for all f âˆˆ S, f(x) = 0
      rw mem_ð•_iff,
      -- so say f âˆˆ S
      intros f hf,
      -- By hypothesis `hx`, it suffices to prove that f âˆˆ S âˆª T
      apply hx,
      -- but this is obvious
      left, assumption
    },
    { -- To prove x âˆˆ ð• T, the argument is the same,
      -- so we write it the way a computer scientist would.
      -- (they prefer one incomprehensible line to four simple ones)
      exact mem_ð•_iff.2 (Î» f hf, hx _ (set.subset_union_right _ _ hf)),
    },
  },
  { -- Step 2: we prove the other inclusion.
    -- âŠ¢ ð• S âˆ© ð• T âŠ† ð• (S âˆª T) (NB `âŠ¢` means "the goal is")
    -- say x is in ð• S and ð• T
    rintro x âŸ¨hxS, hxTâŸ©,
    -- We need to show that for all f âˆˆ S âˆª T, f(x) = 0
    rw mem_ð•_iff,
    -- so choose f in S âˆª T
    intros f hf,
    -- Well, f is either in S or in T, so there are two cases.
    cases hf,
    { -- Say f âˆˆ S
      -- Recall that x âˆˆ ð• S, so âˆ€ f âˆˆ S, f(x) = 0
      rw mem_ð•_iff at hxS,
      -- so we're done.
      exact hxS f hf
    },
    { -- Say f âˆˆ T
      -- The argument is the same so we do it in one step
      exact hxT f hf,
    }
  }
end

-- Infinite (or rather, arbitrary) unions work just the same
-- We consider a collection Sáµ¢ of subsets indexed by i âˆˆ I.
theorem ð•_Union {I : Type*} (S : I â†’ set (mv_polynomial n k)) :
ð• (â‹ƒ i, S i) = â‹‚ i, ð• (S i) :=
begin
  -- To prove equality of two subsets of kâ¿ it suffices to prove âŠ† and âŠ‡.
  apply set.subset.antisymm,
  { -- Goal: ð• (â‹ƒ i, S i) âŠ† â‹‚ i, ð• (S i)
    -- Let x be in the left hand side
    intros x hx,
    -- it suffices to prove that for all j, x âˆˆ ð• (S j) 
    rw set.mem_Inter,
    -- so choose some j âˆˆ I
    intro j,
    -- and say f âˆˆ S j.
    intros f hf,
    -- We now want to prove f(x) = 0.
    -- Now we know x âˆˆ ð• (â‹ƒ i, S i), so g(x) = 0 for all g in â‹ƒ i, S i
    -- Hence it suffices to prove that f âˆˆ â‹ƒ i, S i
    apply hx,
    -- By definition of the infinite union, it suffices to find
    -- some i âˆˆ I such that f âˆˆ S i
    rw set.mem_Union,
    -- and we can use j for this i
    use j,
    -- and what we need to show is true now by assumption, because f âˆˆ S j
    assumption
  },
  { -- Now the other way.
    -- âŠ¢ (â‹‚ (i : I), ð• (S i)) âŠ† ð• (â‹ƒ (i : I), S i)
    -- Say x is in the left hand side
    intros x hx,
    -- It suffices to show that for all f âˆˆ â‹ƒ i, S i, f(x) = 0
    rw mem_ð•_iff,
    -- so say f is a polynomial in this union
    intros f hf,
    -- If f is in the union, then it's in one of the S i, so say f âˆˆ S j
    rw set.mem_Union at hf,
    cases hf with j hj,
    -- Now we know x is in the intersection of the ð• (S i) for all i,
    -- so x âˆˆ ð• (S j)
    rw set.mem_Inter at hx,
    have hxj := hx j,
    -- and because h(x) = 0 for every element h âˆˆ S j, 
    -- and we know f âˆˆ S j, we deduce f(x) = 0 as required.
    exact hxj _ hj
  }
end

-- For convenience, let's define multiplication on subsets of k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™]
-- in the obvious way: S * T := {s * t | s âˆˆ S, t âˆˆ T}.
instance : has_mul (set (mv_polynomial n k)) :=
âŸ¨Î» S T, {u | âˆƒ (s âˆˆ S) (t âˆˆ T), u = s * t}âŸ©

-- For this theorem, we need that k satisfies a * b = 0 => a = 0 or b = 0
theorem ð•_mul {k : Type*} [integral_domain k] {n : Type*}
  (S T : set (mv_polynomial n k)) :
ð• (S * T) = ð• S âˆª ð• T :=
begin
  -- to prove that the two sets are equal we will prove âŠ† and âŠ‡ 
  apply set.subset.antisymm,
  { -- This is the "harder" of the two inclusions;
    -- we need to check that if x vanishes on every element of S*T, 
    -- then x âˆˆ ð• S or x âˆˆ ð• T. So let x be in ð• (S * T)
    intros x hx,
    -- We then know that for every f âˆˆ S * T, f(x) = 0
    rw mem_ð•_iff at hx,
    -- Note for logicians: in this proof, we will assume
    -- the law of the excluded middle.
    classical, 
    -- If x âˆˆ ð• S then the result is easy...
    by_cases hx2 : x âˆˆ ð• S,
      -- because ð• S âŠ† ð• S âˆª ð• T
      exact subset_union_left _ _ hx2,
    -- ...so we can assume assume x âˆ‰ ð• S,
    -- and hence that there's s âˆˆ S such that s(x) â‰  0
    rw mem_ð•_iff at hx2, push_neg at hx2, rcases hx2 with âŸ¨s, hs, hsxâŸ©,
    -- we now show x âˆˆ ð• T,
      right,
    -- i.e., that for all t âˆˆ T we have t(x) = 0
    rw mem_ð•_iff,
    -- So say t âˆˆ T
    intros t ht,
    -- We want to prove that t(x) = 0.
    -- Now by assumption, x vanishes on s * t. 
    replace hx := hx (s * t) âŸ¨s, hs, t, ht, rflâŸ©,
    -- so s(x) * t(x) = 0
    rw eval_mul at hx,
    -- so either s(x) or t(x) = 0,
    cases mul_eq_zero.1 hx with hxs hxt,
      -- So the case s(x) = 0 is a contradiction
      contradiction,
    -- and t(x) = 0 is what we wanted to prove
    assumption
  },
  { -- Here's the easier of the two inclusions.
    -- say x âˆˆ ð• S âˆª ð• T,
    intros x hx,
    -- it's either in ð• S or ð• T.
    cases hx with hxS hxT,
    { -- Say x âˆˆ ð• S.
      -- We know that x vanishes at every element of S.
      rw mem_ð•_iff at hxS,
      -- We want to prove x vanishes at every polynomial of the form s * t
      -- with s âˆˆ S and t âˆˆ T.
      rw mem_ð•_iff,
      -- so let's take a polynomial of the form s * t
      rintro _ âŸ¨s, hs, t, ht, rflâŸ©,
      -- we need to show st(x)=0. So it suffices to show s(x)*t(x)=0
      rw eval_mul,
      -- Because x âˆˆ ð• S, we have s(x)=0.
      replace hxS := hxS s hs,
      -- so it suffices to show 0 * t(x) = 0
      rw hxS,
      -- but this is obvious
      apply zero_mul, 
    },
    { -- This is the case x âˆˆ ð• T and it's of course completely analogous.
      -- If I knew more about Lean's `WLOG` tactic I might not have to do
      -- this case. I'll just do it the computer science way (i.e., a proof
      -- which is quick and incomprehensible)
      rintro _ âŸ¨s, hs, t, ht, rflâŸ©,
      rw [eval_mul, hxT t ht, mul_zero],
    }
  }
end

-- Pedantic exercise: we assumed a * b = 0 => a = 0 or b = 0. Give an
-- example of a commutative ring with that property which is not an
-- integral domain. Is the theorem still true for this ring?

-- Questions or comments? You can often find me on the Lean chat
-- at https://leanprover.zulipchat.com (login required,
-- real names preferred, be nice)

-- Prove a theorem. Write a function. xenaproject.wordpress.com
end affine_algebraic_set
