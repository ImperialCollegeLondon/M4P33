/-
Copyright (c) 2020 Kevin Buzzard
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, and whoever else wants to join in.
-/

-- imports the theory of multivariable polynomials over rings
import data.mv_polynomial

/-!
# The ğ• construction

Let k be a commutative ring and let n be a natural number.

This file defines the map ğ• from subsets of k[Xâ‚,Xâ‚‚,â€¦,Xâ‚™]
to affine algebraic sets in kâ¿, and the map ğ•€ from affine algebraic
sets in kâ¿ to ideals of k[Xâ‚,Xâ‚‚,â€¦, Xâ‚™]. It proves basic properties
about them which can be shown without the Nullstellensatz.

To get ğ• in VS Code, type `\bbV`.

Note: we never assume that the number of variables is finite,
so actually instead of using a natural number n, we use an
arbitrary set N for our variables.

All the definitions work for k a commutative ring, but hardly any
of the the theorems do. But computer scientists want us to set
up the theory for commutative rings and mathematicians should
learn to think more like computer scientists. 
So k starts off being a commutative ring, and only becomes a field
when it has to be, and only becomes an algebraically closed field
when it has to be.

Remark: `analysis/complex/polynomial.lean` contains the proof that â„‚ is algebrically
closed.

Exercise: how do you think you type â„‚ in VS Code?

## Important definitions

* `ğ• : set (mv_polynomial n k) â†’ set (n â†’ k)` 
  sending a subset S of k[Xâ‚,Xâ‚‚,â€¦Xâ‚™] to the subset of kâ¿ cut out by the zeros of all
  the elements of S.

## 

## References

Martin Orr's lecture notes at https://homepages.warwick.ac.uk/staff/Martin.Orr/2017-8/alg-geom/

## Tags

algebraic geometry, algebraic variety, ğ•
-/

-- start of file 
-- We're dealing with multivariable polynomials so let's open the namespace to get
-- easy access to all the functions
open mv_polynomial

-- let k be a commutative ring
variables {k : Type*} [comm_ring k]

-- and let n be any set, but pretend it's {1,2,...,n}.
-- We'll work with polynomials in variables X_i for i âˆˆ n.
variable {n : Type*}

/- Interlude: Lean 3 notation hell.

* the multivariable polynomial ring k[Xâ‚, Xâ‚‚, ..., Xâ‚™] is denoted
  `mv_polynomial n k`.

* The set kâ¿ is denoted
  `n â†’ k`
  (which means maps from n to k, and if you're thinking
   about n as {1,2,3,...,n} then you can see that this makes sense).

* subsets of a set X are denoted
  `set X`

* To evaluate a polynomial f on a vector x, we seem to have to write
  `eval x f`
  Note the order! "Maps on the right".

-/

/-- ğ• : the function sending a subset of k[Xâ‚,Xâ‚‚,â€¦Xâ‚™] to an
  affine algebraic subset of kâ¿, define in Martin Orr's notes -/
def ğ• (S : set (mv_polynomial n k)) : set (n â†’ k) :=
{x : n â†’ k | âˆ€ f âˆˆ S, eval x f = 0}

-- Now let's prove a bunch of theorems about ğ•, in a namespace

namespace affine_algebraic_set.ğ•

variables (S T : set (mv_polynomial n k))
variable (x : n â†’ k)

/-- x âˆˆ ğ• S â†” for all f âˆˆ S, f(x) = 0. This is true by definition. -/
lemma mem_iff {S : set (mv_polynomial n k)} {x : n â†’ k} :
  x âˆˆ ğ• S â†” âˆ€ f âˆˆ S, eval x f = 0 := iff.rfl

/-- If S âŠ† T then ğ•(T) âŠ† ğ•(S) -/
theorem antimono (S T : set (mv_polynomial n k)) :
  S âŠ† T â†’ ğ• T âŠ† ğ• S :=
begin
-- Say S âŠ† T and x âˆˆ ğ• T. 
  intro hST,
  intros x hx,
  -- We want to prove x âˆˆ ğ• S.
  -- We know that âˆ€ t âˆˆ T, t(x) = 0, and we want to prove that âˆ€ s âˆˆ S, s(x) = 0. 
  rw mem_iff at hx âŠ¢,
  -- So say s âˆˆ S.
  intros s hs,
  -- then s âˆˆ T so we're done
  exact hx _ (hST hs),
end

theorem union (S T : set (mv_polynomial n k)) :
ğ• (S âˆª T) = ğ• S âˆ© ğ• T :=
begin
  -- let's prove this by proving âŠ† and âŠ‡
  apply set.subset.antisymm,
  { -- goal : ğ• (S âˆª T) âŠ† ğ• S âˆ© ğ• T
    -- so let x be an element of the LHS
    intros x hx,
    -- then x âˆˆ ğ• (S âˆª T) so âˆ€ f âˆˆ S âˆª T, f(x) = 0. Call this hypothesis `hx`.
    rw mem_iff at hx,
    -- To prove x âˆˆ ğ• S âˆ© ğ• T, it suffices to prove x âˆˆ ğ• S and x âˆˆ ğ• T
    split,
    { -- To prove x âˆˆ ğ• S, we need to show that for all f âˆˆ S, f(x) = 0
      rw mem_iff,
      -- so say f âˆˆ S
      intros f hf,
      -- By hypothesis `hx`, it suffices to prove that f âˆˆ S âˆª T
      apply hx,
      -- but this is obvious
      left, assumption
    },
    { -- To prove x âˆˆ ğ• T, the argument is the same,
      -- so we write it the way a computer scientist would.
      -- (they prefer one line to four)
      exact mem_iff.2 (Î» f hf, hx _ (set.subset_union_right _ _ hf)),
    },
  },
  { -- âŠ¢ ğ• S âˆ© ğ• T âŠ† ğ• (S âˆª T) (NB `âŠ¢` means "the goal is")
    -- say x is in ğ• S and ğ• T
    rintro x âŸ¨hxS, hxTâŸ©,
    -- We need to show that for all f âˆˆ S âˆª T, f(x) = 0
    rw mem_iff,
    -- so choose f in S âˆª T
    intros f hf,
    -- Well, f is either in S or in T, so there are two cases.
    cases hf,
    { -- Say f âˆˆ S
      -- Recall that x âˆˆ ğ• S, so âˆ€ f âˆˆ S, f(x) = 0
      rw mem_iff at hxS,
      -- so we're done.
      exact hxS f hf
    },
    { -- Say f âˆˆ T
      -- The argument is the same so we do it in one step
      exact hxT f hf,
    }
  }
end

end affine_algebraic_set.ğ•
